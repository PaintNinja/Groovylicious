import io.github.groovymc.modsdotgroovy.ConvertToTomlTask

plugins {
    id 'groovy'
    id 'eclipse'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '5.1.+'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
    id 'io.github.groovymc.modsdotgroovy' version '1.1.2'
}

modsDotGroovy {
    dslVersion = '1.3.0'
    automaticConfiguration = false
}

version = '1.19.4-0.4.0-SNAPSHOT'
group = 'ga.ozli.minecraftmods.groovylicious'
archivesBaseName = 'groovylicious'

// Mojang ships Java 17 to end users in 1.18+, so your mod should target Java 17.
java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"

minecraft {
    mappings channel: 'parchment', version: '1.19.3-2023.03.12-1.19.4'

    //accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        configureEach {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'info'
            property 'forge.enabledGameTestNamespaces', archivesBaseName

            mods {
                groovylicious {
                    source sourceSets.main
                }
                test {
                    source sourceSets.test
                }
            }
        }

        client { args '--username', 'Dev##' }
        server { args '--nogui' }
    }
}

sourceSets {
    transform {
        groovy {
            srcDir 'src/transform/groovy'
        }

        resources.srcDirs = []
        resources {
            srcDir 'src/transform/resources'
        }

        compileClasspath += configurations.getByName('minecraft')
    }

    main {
        groovy {
            srcDir 'src/main/groovy'
        }

        resources {
            srcDir 'src/main/resources'
        }

        compileClasspath += sourceSets.transform.output
    }

    test {
        compileClasspath += sourceSets.transform.output
    }
}

configurations {
    testCompileOnly.extendsFrom(compileOnly)
}

// Groovylicious is written in pure Groovy, so delete the empty java folder in each of the sourceSets
sourceSets.each { sourceSet ->
    final javaDir = file("src/${sourceSet.name}/java/")
    javaDir.deleteDir()
}
// let's also delete the unused generated dir while we're at it
final generatedDir = file("src/generated/")
generatedDir.deleteDir()

repositories {
    maven {
        name 'Curse Maven'
        url 'https://www.cursemaven.com'
        content {
            includeGroup 'curse.maven'
        }
    }
    maven {
        name 'Modding Inquisition Snapshots'
        url 'https://maven.moddinginquisition.org/snapshots'
    }
    maven {
        name 'Modding Inquisition Releases'
        url 'https://maven.moddinginquisition.org/releases'
    }
    mavenLocal()
}

dependencies {
    // Forge
    minecraft 'net.minecraftforge:forge:1.19.4-45.0.49'

    // Matyrobbrt's GroovyModLoader
    compileOnly 'com.matyrobbrt.gml:gml:3.1.3'
    runtimeOnly('com.matyrobbrt.gml:gml:3.1.3:all') {
        exclude module: 'transform'
    }
    transformCompileOnly 'com.matyrobbrt.gml:gml:3.1.3'
    transformCompileOnly 'com.matyrobbrt.gml:transform:3.1.3'

    // GroovyMC's CommonGroovyLibrary
    compileOnly 'io.github.groovymc.cgl:cgl-1.19.4-forge:0.2.5'
    transformCompileOnly 'io.github.groovymc.cgl:cgl-1.19.4-forge:0.2.5'

    // Matyrobbrt's EnhancedGroovy for better IDE support
    transformCompileOnly 'com.matyrobbrt.enhancedgroovy:dsl:0.2.0'
}

processResources {
    duplicatesStrategy DuplicatesStrategy.EXCLUDE
}

tasks.named('jar', Jar) {
    from sourceSets.transform.output
    from sourceSets.main.output

    manifest {
        attributes([
                'Specification-Title': archivesBaseName,
                'Specification-Vendor': 'Paint_Ninja',
                'Specification-Version': '1',
                'Implementation-Title': archivesBaseName,
                'Implementation-Version': project.jar.archiveVersion,
                'Implementation-Vendor': 'Paint_Ninja',
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }

    finalizedBy 'reobfJar'
}

//tasks.register('extensionsJar', Jar) {
//    from sourceSets.extension.output
//
//    manifest {
//        attributes(defaultManifest + [
//                'FMLModType': 'GAMELIBRARY'
//        ])
//    }
//
//    finalizedBy 'reobfJar'
//}

tasks.register('sourcesJar', Jar) {
    dependsOn compileGroovy

    archiveClassifier = 'sources'

    from sourceSets.transform.allSource
    from sourceSets.main.allSource

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifacts = [jar, sourcesJar]
        }
    }
    repositories {
        maven {
            name = 'ModdingInquisitionMavenRepo'
            url = 'https://maven.moddinginquisition.org/snapshots'
            credentials {
                username = findProperty('inquisitionMavenUser') ?: ''
                password = findProperty('inquisitionMavenPassword') ?: ''
            }
        }
    }
}

tasks.create('testModsDotGroovyToToml', ConvertToTomlTask) {
    it.configureForSourceSet(sourceSets.test)
}
tasks.create('modsDotGroovyToToml', ConvertToTomlTask) {
    it.configureForSourceSet(sourceSets.main)
}
build.dependsOn 'modsDotGroovyToToml'

tasks.withType(GroovyCompile).configureEach {
    options.incremental = true
    groovyOptions.encoding = 'UTF-8'
    groovyOptions.optimizationOptions.indy = true
    groovyOptions.optimizationOptions.groovydoc = true
    groovyOptions.configurationScript = file('src/transform/groovy/ga/ozli/minecraftmods/groovylicious/transform/groovycConfig.groovy')
}
